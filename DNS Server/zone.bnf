;
;   Rule descriptions and some definitions copied from <http://tools.ietf.org/html/rfc2616>
;
;   Matches must be performed greedily, the rule which matches the most characters is used
;   
;   name = definition
;      The name of a rule is simply the name itself (without any
;      enclosing "<" and ">") and is separated from its definition by the
;      equal "=" character. White space is only significant in that
;      indentation of continuation lines is used to indicate a rule
;      definition that spans more than one line. Certain basic rules are
;      in uppercase, such as SP, LWS, HT, CRLF, DIGIT, ALPHA, etc. Angle
;      brackets are used within definitions whenever their presence will
;      facilitate discerning the use of rule names.
;
;   "literal"
;      Quotation marks surround literal text. Unless stated otherwise,
;      the text is case-insensitive.
;
;   rule1 | rule2
;      Elements separated by a bar ("|") are alternatives, e.g., "yes |
;      no" will accept yes or no.
;
;   (rule1 rule2)
;      Elements enclosed in parentheses are treated as a single element.
;      Thus, "(elem (foo | bar) elem)" allows the token sequences "elem
;      foo elem" and "elem bar elem".
;
;   *rule
;      The character "*" preceding an element indicates repetition. The
;      full form is "<n>*<m>element" indicating at least <n> and at most
;      <m> occurrences of element. Default values are 0 and infinity so
;      that "*(element)" allows any number, including zero; "1*element"
;      requires at least one; and "1*2element" allows one or two.
;   [rule]
;      Square brackets enclose optional elements; "[foo bar]" is
;      equivalent to "*1(foo bar)".
;   

zone		= *line

line		= ( directive | record | blank ) LWS [ comment ] eol
eol			= newline | eof

directive	= "$" ( origin | ttl )

origin		= "ORIGIN" 1*WS fqdn

ttl			= "TTL" 1*WS ttl-param
ttl-param	= ( ttl-value | 1*( ttl-value ttl-unit ) )
ttl-value	= 1*DIGIT
ttl-unit	= "w" | "d" | "h" | "m" | "s"

;	syntactically <rdata> is the only required field, semantically the preceeding record(s) can provide the values for <name>, <class> and <type>, if there are no preceding records the zone is invalid
record		= ( ( name 1*WS ) | 1*WS ) [ ttl-param 1*WS ] [ class 1*WS ] type 1*WS rdata

;	incomplete list of class types, but we're unlikely to need to return Chaosnet or Hesiod class records
class		= "IN"

;	incomplete list of record types, but we're unlikely to need to return those unlisted, support should be easy to add if need be
;	adding support for new record types will require a type specific semantic validator (syntactically their data should already be supported by the rdata rule)
type		= "A" | "AAAA" | "MX" | "NS" | "PTR" | "SOA" | "SRV" | "TXT" | "CNAME" | "NAPTR" | "SPF"

;	<rdata> is 1 or more <data> productions, if multiple they are whitespace separated, the newline from <line> terminates the listing
rdata		= data *( 1*WS data )
;	<data> can be a single <data-field> or, muliple <data-field> productions grouped by "(" ")" optionally split across newlines
;	parsing <data> yields 1 or more <data-field>
data		= data-field | ( "(" 1*( LWS ( data-field | comment | newline ) ) ")" )
;	if whitespace is semantically important to a data-field value it must be quoted-data to distinquish it from ignored whitespace between fields
data-field	= quoted-data | 1*inner-data
;	quoted-data can contain whitespace, maximum length of quoted-data is 255 characters
quoted-data = <"> *( inner-data | common-excluded | quoted-pair ) <">
;	if data for a specific resource contains one of the excluded literal characters it must use quoted-data or they'll be confused for a separator
inner-data	= <any TEXT except excluded>
excluded = <"> | common-excluded
common-excluded = WS | ";" | "(" | ")"
quoted-pair	= "\" CHAR

blank		= LWS

;	either @ to refer to the origin or, a domain name which has the origin appended to make it fully qualified or a fully qualified domain name, both optionally with a wildcard prefix
;	a wildcard domain label can only appear as the first label hence it isn't in the <label> rule definition
name		= "@" | "*" | ( [ "*" "." ] ( dn | fqdn ) )
;	NB: ensure the longest match is used so that a <dn> isn't scanned where an <fqdn> is provided, the only difference is the trailing "."
dn			= label *( "." label )
fqdn		= 1*( label "." )
;	wildcard labels are only supported as a prefix in <name>, not as a subsequent <label> in a <name>
label		= 1*( ALPHA | DIGIT | "-" | "_" )
CHAR		= <any US-ASCII character (octets 0 - 127)>
UPALPHA		= <any US-ASCII uppercase letter "A".."Z">
LOALPHA		= <any US-ASCII lowercase letter "a".."z">
ALPHA		= UPALPHA | LOALPHA
DIGIT		= <any US-ASCII digit "0".."9">

comment		= ";" *TEXT
TEXT		= <any OCTET except CTLs, but including WS>
CTL			= <any US-ASCII control character (octets 0 - 31) and DEL (127)>

newline		= LF | ( CR LF )
CR			= <US-ASCII CR, carriage return (13)>
LF			= <US-ASCII LF, linefeed (10)>
;	LWS is implicitly optional, minimum of 0 maximum of infinite
LWS			= *WS
WS			= SP | HT
SP			= <US-ASCII SP, space (32)>
HT			= <US-ASCII HT, horizontal-tab (9)>
